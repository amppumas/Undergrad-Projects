********************************************************
*Cash register for MC68HC11 microprocessor*
* Meza Peña Augusto                                    *
* Moreno Tagle Raphael Iván                            *
* Muñoz Alvarez Rosa María Yolotzin                    *
********************************************************

**********************************
*    VARIABLES PUERTO SERIAL     *
**********************************
OPTION EQU   $1039
DDRD   EQU   $1009
SCDR   EQU   $102F
SCCR2  EQU   $102D
SCSR   EQU   $102E
SCCR1  EQU   $102C
BAUD   EQU   $102B
HPRIO  EQU   $103C
***********************************
* DECLARACION DE VARIABLES        *
***********************************
ORDEN    EQU $0000
TOTALA   EQU $0002
TOTALB	 EQU $0004
TOTALC	 EQU $0006
TOTALD	 EQU $0008
TOTALE	 EQU $000a
TOTAL	 EQU $000c
U1	 EQU $0010
DIR_BASE EQU $0011
U3	 EQU $0012
U4	 EQU $0013
U5	 EQU $0014
PRINTADD EQU $0015
AUX	 EQU $0017
VAR      EQU $0019
CANT_A   EQU $001a
CANT_B   EQU $001b
CANT_C   EQU $001c
CANT_D   EQU $001d
CANT_E   EQU $001e
SHARP	 EQU $001f
***********************************
*  CUERPO  DEL PROGRAMA           *
***********************************
	ORG   $8000
***STRINGS con los numeros en letra y mensajes de error*****
ERR	FCC	"     ERROR      n"
ESPA	FCC	")x50    n"
ESPB	FCC	")x100   n"
ESPC	FCC	")x200   n"
ESPD	FCC	")x300   n"
ESPE	FCC	")x400   n"
ART	FCC	"ART:n"
TOT	FCC	"  TOT:n"
CANTA	FCC	"A(n"
CANTB	FCC	"B(n"
CANTC	FCC	"C(n"
CANTD	FCC	"D(n"
CANTE	FCC	"E(n"
TICKET  FCC	"     TICKET     n"
YCINCO	FCC	" Y CINCOn"
CINCO	FCC	"CINCOn"
DIEZ	FCC	"DIEZn"
QUINCE	FCC	"QUINCEn"
VEINTE	FCC	"VEINTEn"
VEICIN	FCC	"VEINTICINCOn"
TREINTA FCC	"TREINTAn"
CUAREN	FCC	"CUARENTAn"
CINCUEN	FCC	"CINCUENTAn"
SESEN	FCC	"SESENTAn"
SETEN	FCC	"SETENTAn"
OCHEN	FCC	"OCHENTAn"
NOVEN	FCC	"NOVENTAn"
CIEN	FCC	"CIENn"
CIENTO	FCC	"CIENTOn"
DOSCIEN	FCC	"DOSCIENTOSn"
TRECIEN	FCC	"TRESCIENTOSn"
CUACIEN	FCC	"CUATROCIENTOSn"
QUICIEN	FCC	"QUINIENTOSn"
SEICIEN	FCC	"SEISCIENTOSn"
SETCIEN	FCC	"SETECIENTOSn"
OCHCIEN	FCC	"OCHOCIENTOSn"
NOVCIEN	FCC	"NOVECIENTOSn"
MIL	FCC	"MILn"
DOSMIL	FCC	"DOS MILn"
TREMIL	FCC	"TRES MILn"
CUAMIL	FCC	"CUATRO MILn"
CINMIL	FCC	"CINCO MILn"
SEIMIL	FCC	"SEIS MILn"
SIEMIL	FCC	"SIETE MILn"
OCHMIL	FCC	"OCHO MILn"
NUEMIL	FCC	"NUEVE MILn"
*************INICIO**************************
INICIO
****************CONF. PUERTO SERIAL**********
	LDS #$00EE
	JSR SERIAL
***************CLR VARIABLES****************
	CLR TOTALA
	CLR $0003
	CLR TOTALB
	CLR $0005
	CLR TOTALC
	CLR $0007
	CLR TOTALD
	CLR $0009
	CLR TOTALE
	ClR $000b
	CLR TOTAL
	CLR $000d
	CLR CANT_A
	CLR CANT_B
	CLR CANT_C
	CLR CANT_D
	CLR CANT_E
	CLR SHARP
	CLR AUX
	LDY #0
**************LEER DEL PUERTO SERIAL**********
ENCICLATE
	LDAA #'?
	STAA ORDEN
CICLO
	LDAA ORDEN
	CMPA #'?
	BEQ CICLO
	CMPA #'+
	BEQ ENCICLATE
	CMPA #'=
	BEQ SALTO
	CMPA #'#
	BEQ RESTAR
	LDAB SHARP
	BGT INRESTAR
*************COMPARA CARACTERES***************
	CMPA #'E
	BLS NOLOWCASE
	JSR LOWCASE
NOLOWCASE
	CMPA #'A
	BEQ SUMAR_A
	CMPA #'B
	BEQ SUMAR_B
	CMPA #'C
	BEQ SUMAR_C
	CMPA #'D
	BEQ SUMAR_D
	CMPA #'E
	BEQ SUMAR_E
	CMPA #'A
	BLO INVALID
	JMP ENCICLATE

LOWCASE
	CMPA #'z
	BHI INVALID
	SUBA #$0020
	RTS

INVALID JMP ERROR
*********Set Bandera de Sharp**********
RESTAR
	INC SHARP
	JMP ENCICLATE
******Saltar a rutina de Imprimir en pantalla**
SALTO
	JMP IMPRIMIR
**************Operaciones de Sumar*************
SUMAR_A
	LDD TOTALA
	ADDD #50
	STD TOTALA
	INC CANT_A
	JMP ENCICLATE

SUMAR_B
	LDD TOTALB
	ADDD #100
	STD TOTALB
	INC CANT_B
	JMP ENCICLATE

SUMAR_C
	LDD TOTALC
	ADDD #200
	STD TOTALC
	INC CANT_C
	JMP ENCICLATE

SUMAR_D
	LDD TOTALD
	ADDD #300
	STD TOTALD
	INC CANT_D
	JMP ENCICLATE

SUMAR_E
	LDD TOTALE
	ADDD #400
	STD TOTALE
	INC CANT_E
	JMP ENCICLATE
**********OPERACIONES DE RESTAR*****************
INRESTAR
	CMPA #'E
	BLS NOLOWCASE2
	JSR LOWCASE
NOLOWCASE2
	CMPA #'A
	BEQ RESTAR_A
	CMPA #'B
	BEQ RESTAR_B
	CMPA #'C
	BEQ RESTAR_C
	CMPA #'D
	BEQ RESTAR_D
	CMPA #'E
	BEQ RESTAR_E
	CMPA #'A
	BLO INVALID
	JMP ENCICLATE

RESTAR_A
	LDD TOTALA
	SUBD #50
	STD TOTALA
	DEC CANT_A
	CLR SHARP
	JMP ENCICLATE

RESTAR_B
	LDD TOTALB
	SUBD #100
	STD TOTALB
	DEC CANT_B
	CLR SHARP
	JMP ENCICLATE

RESTAR_C
	LDD TOTALC
	SUBD #200
	STD TOTALC
	DEC CANT_C
	CLR SHARP
	JMP ENCICLATE

RESTAR_D
	LDD TOTALD
	SUBD #300
	STD TOTALD
	DEC CANT_D
	CLR SHARP
	JMP ENCICLATE

RESTAR_E
	LDD TOTALE
	SUBD #400
	STD TOTALE
	DEC CANT_E
	CLR SHARP
	JMP ENCICLATE
*************************DESCUENTOS***********************
DESCUENTOS
	JSR DESCUENTOSA
	JSR DESCUENTOSB
	JSR DESCUENTOSC
	JSR DESCUENTOSD
	JSR DESCUENTOSE
	RTS
****************************Divide el acumulador D entre 2*************************
DIVIDIR
	LDX #2
	IDIV
	XGDX
	RTS
****************************DESCUENTOS de A*************************
DESCUENTOSA
	LDAB CANT_A
	CMPB #2
	BEQ VEINTEA
	CMPB #3
	BEQ TREINTAA
	CMPB #4
	BEQ CUARENTAA
	CMPB #5
	BHS CINCUENTAA
	RTS
VEINTEA
	LDD #80
	STD TOTALA
	RTS
TREINTAA
	LDD #105
	STD TOTALA
	RTS
CUARENTAA
	LDD #120
	STD TOTALA
	RTS

CINCUENTAA
	LDD TOTALA
	JSR DIVIDIR
	STD TOTALA
	RTS
**************************DESCUENTOS de B*************************
DESCUENTOSB
	LDAB CANT_B
	CMPB #2
	BEQ VEINTEB
	CMPB #3
	BEQ TREINTAB
	CMPB #4
	BEQ CUARENTAB
	CMPB #5
	BHS CINCUENTAB
	RTS
VEINTEB
	LDD #160
	STD TOTALB
	RTS
TREINTAB
	LDD #210
	STD TOTALB
	RTS
CUARENTAB
	LDD #240
	STD TOTALB
	RTS

CINCUENTAB
	LDD TOTALB
	JSR DIVIDIR
	STD TOTALB
	RTS
*************************DESCUENTOS de C*************************
DESCUENTOSC
	LDAB CANT_C
	CMPB #2
	BEQ VEINTEC
	CMPB #3
	BEQ TREINTAC
	CMPB #4
	BEQ CUARENTAC
	CMPB #5
	BHS CINCUENTAC
	RTS
VEINTEC
	LDD #320
	STD TOTALC
	RTS
TREINTAC
	LDD #420
	STD TOTALC
	RTS
CUARENTAC
	LDD #480
	STD TOTALC
	RTS

CINCUENTAC
	LDD TOTALC
	JSR DIVIDIR
	STD TOTALC
	RTS
*************************DESCUENTOS de D*************************
DESCUENTOSD
	LDAB CANT_D
	CMPB #2
	BEQ VEINTED
	CMPB #3
	BEQ TREINTAD
	CMPB #4
	BEQ CUARENTAD
	CMPB #5
	BHS CINCUENTAD
	RTS
VEINTED
	LDD #480
	STD TOTALD
	RTS
TREINTAD
	LDD #630
	STD TOTALD
	RTS
CUARENTAD
	LDD #720
	STD TOTALD
	RTS

CINCUENTAD
	LDD TOTALD
	JSR DIVIDIR
	STD TOTALD
	RTS
**************************DESCUENTOS de E*************************
DESCUENTOSE
	LDAB CANT_E
	CMPB #2
	BEQ VEINTEE
	CMPB #3
	BEQ TREINTAE
	CMPB #4
	BEQ CUARENTAE
	CMPB #5
	BHS CINCUENTAE
	RTS
VEINTEE
	LDD #640
	STD TOTALE
	RTS
TREINTAE
	LDD #840
	STD TOTALE
	RTS
CUARENTAE
	LDD #960
	STD TOTALE
	RTS

CINCUENTAE
	LDD TOTALE
	JSR DIVIDIR
	STD TOTALE
	RTS
******Subrutina que despliega un mensaje de Error*************
ERROR
	JSR CLR
	LDX #ERR
	LDD #$0080
	STD PRINTADD
	JSR PRINT
	JMP NOPLOOP
NOPLOOP
	JMP NOPLOOP
******TERMINA LA OPERACION Y CALCULA LOS TOTALES*************
IMPRIMIR
	JSR DESCUENTOS
	LDD #0
	ADDD TOTALA
	CPD #$270f
	BHI ERROR
	ADDD TOTALB
	CPD #$270f
	BHI ERROR
	ADDD TOTALC
	CPD #$270f
	BHI ERROR
	ADDD TOTALD
	CPD #$270f
	BHI ERROR
	ADDD TOTALE
	CPD #$270f
	BHI ERROR
	STD TOTAL
	JSR CLR
	JMP MILES
****Subrutina que convierte el acumulador D a decimal*****
DECIMAL
	CLR U1
	CLR DIR_BASE
	CLR U3
	CLR U4
	CLR U5
	CPD #0
	BEQ FIN
BINBCD
     	LDX  #$2710
      	IDIV
      	XGDX
      	STAB U1
      	XGDX
      	LDX  #$3E8
      	IDIV
      	XGDX
      	STAB DIR_BASE
      	XGDX
BNBCD
      	LDX  #$64
      	IDIV
      	XGDX
      	STAB U3
      	XGDX
      	LDX  #$A
      	IDIV
      	XGDX
      	STAB U4
      	XGDX
      	STAB U5
FIN	RTS
*******BORRA LA PANTALLA DESDE $0020 a $00e0 ************
CLR
	LDY #$0020
	LDAA #$ff
BORRAR
	STAA 0,Y
	INY
	CPY #$00e0
	BEQ FINCLR
	JMP BORRAR
FINCLR
	RTS
********Rutinas de Impresion de numeros de 2 y 4 digitos*************
PRINT4
	LDX #0
	LDY PRINTADD
LOOP	CPX #4
	BEQ FIN_PRINT4
	LDAA DIR_BASE,X
	ADDA #$30
	STAA 0,Y
	INX
	INY
	JMP LOOP
FIN_PRINT4
	RTS

PRINT2
	LDX #2
	LDY PRINTADD
LOOP2	CPX #4
	BEQ FIN_PRINT2
	LDAA DIR_BASE,X
	ADDA #$30
	STAA 0,Y
	INX
	INY
	JMP LOOP2
FIN_PRINT2
	STY PRINTADD
	RTS
*************EMPIEZA A IMPRIMIR EL TICKET******************
MILES
************IMPRIME EL FORMATO DEL TICKET*********************
	LDD #$0020
	STD PRINTADD
	LDX #TICKET
	JSR PRINT
*******************Linea de las A************************
	LDD #$0030
	STD PRINTADD
	LDX #CANTA
	JSR PRINT
	LDAB CANT_A
	CLRA
	JSR DECIMAL
	JSR PRINT2
	LDX #ESPA
	JSR PRINT
	LDD TOTALA
	JSR DECIMAL
	JSR PRINT4
*******************Linea de las B************************
	LDD #$0040
	STD PRINTADD
	LDX #CANTB
	JSR PRINT
	LDAB CANT_B
	CLRA
	JSR DECIMAL
	JSR PRINT2
	LDX #ESPB
	JSR PRINT
	LDD TOTALB
	JSR DECIMAL
	JSR PRINT4
*******************Linea de las C************************
	LDD #$0050
	STD PRINTADD
	LDX #CANTC
	JSR PRINT
	LDAB CANT_C
	CLRA
	JSR DECIMAL
	JSR PRINT2
	LDX #ESPC
	JSR PRINT
	LDD TOTALC
	JSR DECIMAL
	JSR PRINT4
*******************Linea de las D************************
	LDD #$0060
	STD PRINTADD
	LDX #CANTD
	JSR PRINT
	LDAB CANT_D
	CLRA
	JSR DECIMAL
	JSR PRINT2
	LDX #ESPD
	JSR PRINT
	LDD TOTALD
	JSR DECIMAL
	JSR PRINT4
*******************Linea de las E************************
	LDD #$0070
	STD PRINTADD
	LDX #CANTE
	JSR PRINT
	LDAB CANT_E
	CLRA
	JSR DECIMAL
	JSR PRINT2
	LDX #ESPE
	JSR PRINT
	LDD TOTALE
	JSR DECIMAL
	JSR PRINT4
*************Linea Articulos y Total en digitos**********
	LDD #$0080
	STD PRINTADD
	LDX #ART
	JSR PRINT
	LDAB CANT_A
	ADDB CANT_B
	ADDB CANT_C
	ADDB CANT_D
	ADDB CANT_E
	LDAA #0
	JSR DECIMAL
	JSR PRINT2
	LDX #TOT
	JSR PRINT
	LDD TOTAL
	JSR DECIMAL
	JSR PRINT4
***************TERMINA formato del ticket******************
	LDD TOTAL
	LDY #3
	JSR DECIMAL
	LDY #0
	LDX #0
	LDD #$00a0
	STD PRINTADD
	LDAA DIR_BASE,Y
	CMPA #1
	BEQ ONET
	CMPA #2
	BEQ TWOT
	CMPA #3
	BEQ THREET
	CMPA #4
	BEQ FOURT
	CMPA #5
	BEQ FIVET
	CMPA #6
	BEQ SIXT
	CMPA #7
	BEQ SEVENT
	CMPA #8
	BEQ EIGHTT
	CMPA #9
	BEQ NINET
	JMP CIENTOS

ONET
	LDX #MIL
	JSR PRINT
	JMP CIENTOS

TWOT
	LDX #DOSMIL
	JSR PRINT
	JMP CIENTOS

THREET
	LDX #TREMIL
	JSR PRINT
	JMP CIENTOS

FOURT
	LDX #CUAMIL
	JSR PRINT
	JMP CIENTOS

FIVET
	LDX #CINMIL
	JSR PRINT
	JMP CIENTOS

SIXT
	LDX #SEIMIL
	JSR PRINT
	JMP CIENTOS

SEVENT
	LDX #SIEMIL
	JSR PRINT
	JMP CIENTOS

EIGHTT
	LDX #OCHMIL
	JSR PRINT
	JMP CIENTOS

NINET
	LDX #NUEMIL
	JSR PRINT
	JMP CIENTOS

************EMPIEZAN LOS CIENTOS****************
CIENTOS
	INY
	LDD #$00b0
	STD PRINTADD
	LDAA DIR_BASE,Y
	CMPA #1
	BEQ ONEH
	CMPA #2
	BEQ TWOH
	CMPA #3
	BEQ THREEH
	CMPA #4
	BEQ FOURH
	CMPA #5
	BEQ FIVEH
	CMPA #6
	BEQ SIXH
	CMPA #7
	BEQ SEVENH
	CMPA #8
	BEQ EIGHTH
	CMPA #9
	BEQ NINEH
	JMP DECENAS
ONEH
	INY
	LDAA DIR_BASE,Y
	CMPA #0
	BEQ UN_CERO
	LDX #CIENTO
	JSR PRINT
	DEY
	JMP DECENAS

UN_CERO
	INY
	LDAA DIR_BASE,Y
	CMPA #0
	BEQ DOS_CEROS
	LDX #CIENTO
	JSR PRINT
	DEY
	DEY
	JMP DECENAS

DOS_CEROS
	LDX #CIEN
	JSR PRINT
	JMP ENCICLATE

TWOH
	LDX #DOSCIEN
	JSR PRINT
	JMP DECENAS

THREEH
	LDX #TRECIEN
	JSR PRINT
	JMP DECENAS

FOURH
	LDX #CUACIEN
	JSR PRINT
	JMP DECENAS

FIVEH
	LDX #QUICIEN
	JSR PRINT
	JMP DECENAS

SIXH
	LDX #SEICIEN
	JSR PRINT
	JMP DECENAS

SEVENH
	LDX #SETCIEN
	JSR PRINT
	JMP DECENAS

EIGHTH
	LDX #OCHCIEN
	JSR PRINT
	JMP DECENAS

NINEH
	LDX #NOVCIEN
	JSR PRINT
	JMP DECENAS
*************ESTA ESTÁ AQUI PARA QUE ALCANZEN LOS SALTOS**
FIVE
	JSR NEXTCMP
	BEQ FIVEV
	JMP ENCICLATE
FIVEV	LDX #CINCO
	JSR PRINT
	JMP ENCICLATE
******************AQUI EMPIEZAN LAS DECENAS**************
DECENAS
	INY
	LDD #$00c0
	STD PRINTADD
	LDAA DIR_BASE,Y
	CMPA #0
	BEQ FIVE
	CMPA #1
	BEQ TEN
	CMPA #2
	BEQ TWENTY
	CMPA #3
	BEQ THIRTY
	CMPA #4
	BEQ FORTY
	CMPA #5
	BEQ FIFTY
	CMPA #6
	BEQ SIXTY
	CMPA #7
	BEQ SEVENTY
	CMPA #8
	BEQ EIGHTY
	CMPA #9
	BEQ NINETY

TEN
	JSR NEXTCMP
	BEQ QNCE
	LDX #DIEZ
	JSR PRINT
	JMP ENCICLATE

TWENTY
	JSR NEXTCMP
	BEQ TWEFIV
	LDX #VEINTE
	JSR PRINT
	JMP ENCICLATE

THIRTY
	LDX #TREINTA
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE


FORTY
	LDX #CUAREN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE

FIFTY
	LDX #CINCUEN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE

SIXTY
	LDX #SESEN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE

SEVENTY
	LDX #SETEN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE

EIGHTY
	LDX #OCHEN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE

NINETY
	LDX #NOVEN
	JSR PRINT
	JSR NEXTCMP
	BEQ ANDFIV
	JMP ENCICLATE
QNCE
	LDX #QUINCE
	JSR PRINT
	JMP ENCICLATE

TWEFIV
	LDX #VEICIN
	JSR PRINT
	JMP ENCICLATE

ANDFIV
	LDX #YCINCO
	LDD PRINTADD
	CPD #$00c8
	BHI NEXTLINE
	JSR PRINT
	JMP ENCICLATE
NEXTLINE
	INX
	LDD #$00d0
	STD PRINTADD
	JSR PRINT
	JMP ENCICLATE

*********ESTA SUBRUTINA IMPRIME EL STRING QUE SE ENCUENTRE
*********EN LA DIRECCION A LA QUE APUNTA EL REGISTRO X
PRINT	LDAA 0,X
	CMPA #'n
	BEQ FINPRNT
	STY AUX
	LDY PRINTADD
	STAA 0,Y
	LDY AUX
	INX
	LDD PRINTADD
	ADDD #1
	STD PRINTADD
	JMP PRINT
FINPRNT	RTS

***********SUBRUTINA PARA AHORRAR LINEAS*****
NEXTCMP
	INY
	LDAA DIR_BASE,Y
	CMPA #5
	RTS
**************************************************
* SUBRUTINA  DE CONFIGURACION PUERTO SERIAL      *
**************************************************
SERIAL
	LDD   #$302C  * CONFIGURA PUERTO SERIAL
	STAA  BAUD    * BAUD  9600  para cristal de 8MHz
	STAB  SCCR2   * HABILITA  RX Y TX PERO INTERRUPCN SOLO RX
	LDAA  #$00
	STAA  SCCR1   * 8 BITS
	LDAA  #$FE    * CONFIG PUERTO D COMO SALIDAS (EXCEPTO PD0)
	STAA  DDRD    * SEA  ENABLE DEL DISPLAY  PD4  Y RS PD3
	LDAA  #$04
	STAA  HPRIO
	LDAA  #$00
	TAP
	RTS
***********************************
* ATENCION A INTERRUPCION SERIAL
***********************************
	ORG  $F100
	LDAA SCSR
	LDAA SCDR
	STAA ORDEN
	DEC  VAR
	RTI
***********************************
* VECTOR INTERRUPCION SERIAL
***********************************
	ORG   $FFD6
	FCB   $F1,$00
***********************************
*RESET
***********************************
	ORG    $FFFE
RESET 	FCB    $80,$00
***********************************
	END   $8000
